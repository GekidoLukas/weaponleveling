plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "dev.architectury:architectury-fabric:${rootProject.architectury_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }

    implementation(shadowCommon(annotationProcessor("com.github.LlamaLad7:MixinExtras:${rootProject.mixinextras_version}")))

    implementation("com.electronwill.night-config:core:${rootProject.night_config_version}")
    implementation("com.electronwill.night-config:toml:${rootProject.night_config_version}")
    modApi("net.minecraftforge:forgeconfigapiport-fabric:${rootProject.forgeconfigapi_version}")

    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"



    modImplementation "curse.maven:bettercombat-639842:${rootProject.bettercombat_fabric_version}" // Better Combat
    modImplementation "curse.maven:clothconfig-348521:${rootProject.clothconfig_fabric_version}" // Cloth Config
    modImplementation "curse.maven:playeranim-658587:${rootProject.playeranim_fabric_version}" // playerAnimator





}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
    relocate("com.llamalad7.mixinextras", "net.geradesolukas.mixinextras")
    relocate("net.minecraftforge", "net.geradesolukas.minecraftforge")
    relocate("com.electronwill.nightconfig", "net.geradesolukas.nightconfig")
    mergeServiceFiles()
}

remapJar {
    injectAccessWidener = true
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = "fabric"
}

jar {
    archiveClassifier = "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-fabric-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
