plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    forge {
        convertAccessWideners = true

        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name

        mixinConfig "weaponleveling-common.mixins.json"
        mixinConfig "weaponleveling.mixins.json"
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.forge_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury-forge:${rootProject.architectury_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }

    forgeRuntimeLibrary(shadowCommon(annotationProcessor("com.github.LlamaLad7:MixinExtras:0.1.1")))


    modCompileOnly "curse.maven:bettercombat-639842:4428885" // Better Combat
    modCompileOnly "curse.maven:clothconfig-348521:3972426" // Cloth Config
    modCompileOnly "curse.maven:playeranim-658587:4418152" // playerAnimator

    modCompileOnly "curse.maven:crayfishgun-289479:4344223" //MrCrayfish's Gun Mod
    modCompileOnly "curse.maven:framework-549225:4287475" //Framework
    //modRuntimeOnly
    //modCompileOnly()
    modCompileOnly "curse.maven:tinkers-74072:4509008" //Tinkers
    modCompileOnly "curse.maven:mantle-74924:4509007" //Mantle

    modCompileOnly "curse.maven:parrythis-479996:3910075" //Parry This!

    modCompileOnly "curse.maven:tetra-289712:4029100" //Tetra
    modCompileOnly "curse.maven:mutil-351914:3788376" //mutil

    //modImplementation "curse.maven:geckolib-388172:4181370"
    modCompileOnly "curse.maven:epic-fight-mod-405076:4922082" // Epic Fight
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"

    relocate("com.llamalad7.mixinextras", "net.geradesolukas.mixinextras")
    mergeServiceFiles()
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = "forge"
}

jar {
    archiveClassifier = "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-forge-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
